plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.4.3"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {url "https://maven.terraformersmc.com/releases"}
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = "Ladysnake Libs"
        url = "https://ladysnake.jfrog.io/artifactory/mods"
    }
    mavenLocal()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modApi("com.terraformersmc:modmenu:${project.modmenu_version}")

    modImplementation "TechReborn:TechReborn-1.16:3.4.5+build.95"

    modImplementation "de.guntram.mcmod:GBfabrictools:${project.gbfabrictools_version}"
    include "de.guntram.mcmod:GBfabrictools:${project.gbfabrictools_version}"

    modImplementation "dev.emi:trinkets:${project.trinkets_version}"
    modApi "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_components_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${archiveBaseName}" }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

tasks.publish.dependsOn build
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName

            // add all the jars that should be included when publishing to maven
            //artifact(jar)             { builtBy remapJar }
            from components.java
        }
    }

    repositories {
        maven {
            url "$projectDir/../../CommonMaven"
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = project.modrinth_id
    versionNumber = project.mod_version
    versionName = "DurabilityViewer ${project.mod_version}"
    versionType = isMCVersionNonRelease() ? "beta" : "release"
    changelog = file("changelog.md").text
    uploadFile = remapJar
    gameVersions = [ project.minecraft_version ]
    loaders = [ "fabric" ]
    dependencies {
        required.project 'fabric-api'
    }
}

boolean isMCVersionNonRelease() {
    return project.minecraft_version.matches('^\\d\\dw\\d\\d[a-z]$') || project.minecraft_version.matches('\\d+\\.\\d+-(pre|rc)(\\d+)')
}
